/*
 * Example 1 from F. Collard (2001): "Stochastic simulations with DYNARE:
 * A practical guide" (see "guide.pdf" in the documentation directory).
 */

/*
 * Copyright (C) 2001-2010 Dynare Team
 *
 * This file is part of Dynare.
 *
 * Dynare is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Dynare is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Dynare.  If not, see <http://www.gnu.org/licenses/>.
 */


var y, c, k, a, h, b;
varexo e, u;

parameters beta, rho, alpha, delta, theta, psi, tau;

alpha = 0.36;
rho   = 0.95;
tau   = 0.025;
beta  = 0.99;
delta = 0.025;
psi   = 0;
theta = 2.95;

phi   = 0.1;

model;
c*theta*h^(1+psi)=(1-alpha)*y;
k = beta*(((exp(b)*c)/(exp(b(+1))*c(+1)))
    *(exp(b(+1))*alpha*y(+1)+(1-delta)*k));
y = exp(a)*(k(-1)^alpha)*(h^(1-alpha));
k = exp(b)*(y-c)+(1-delta)*k(-1);
a = rho*a(-1)+tau*b(-1) + e;
b = tau*a(-1)+rho*b(-1) + u;
end;

initval;
y = 1.08068253095672;
c = 0.80359242014163;
h = 0.29175631001732;
k = 11.08360443260358;
a = 0;
b = 0;
e = 0;
u = 0;
end;

shocks;
var e; stderr 1;
var u; stderr 1;
end;

stoch_simul(irf=20,order=1);
unit_irf=cell2mat(struct2cell(oo_.irfs));

shocks;
var e; stderr 0.005;
var u; stderr 0.005;
end;

stoch_simul(irf=20,order=1,relative_irf);
relative_irfs=cell2mat(struct2cell(oo_.irfs));
if max(max(abs(100*unit_irf-relative_irfs)))>1e-8;
     error('relative_irf-option at order=1 is broken')
end


//Check relative IRF option at order 2 by comparing it with unnormalized IRF of unit size 
shocks;
var e; stderr 0.01;
var u; stderr 0.01;
end;
set_dynare_seed('default');
options_.relative_irf=0;
stoch_simul(irf=20,order=2);
unit_irf_order_2=cell2mat(struct2cell(oo_.irfs));

shocks;
var e; stderr 0.0095;
var u; stderr 0.0095;
end;
set_dynare_seed('default');
stoch_simul(irf=20,order=2,relative_irf);
relative_irfs_order_2=cell2mat(struct2cell(oo_.irfs));
if max(max(abs(unit_irf_order_2-relative_irfs_order_2)))>1e-4;
     error('relative_irf-option at order=2 is broken')
end

             